buildscript {
    apply from: rootProject.file('gradle/dependencies.gradle')

    repositories {
        jcenter()
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version '1.3.71' apply false
    id 'org.jmailen.kotlinter' version '2.2.0' apply false
    id 'binary-compatibility-validator' version '0.2.3'
}

subprojects {
    repositories {
        jcenter()
        maven {
            url = "https://maven.pkg.github.com/juullabs/android-github-packages"
            credentials {
                username findProperty("github.packages.username")
                password findProperty("github.packages.password")
            }
        }
        mavenLocal()
    }
}

subprojects {
    tasks.withType(Test) {
        testLogging {
            events "started", "passed", "skipped", "failed", "standardOut", "standardError"
            exceptionFormat "full"
            showExceptions true
            showStackTraces true
            showCauses true
        }
    }
}

// Prevent publishing if `version` property is not set (e.g. via `-Pversion` command line argument).
gradle.taskGraph.whenReady { taskGraph ->
    taskGraph.getAllTasks().findAll { task ->
        task.name.startsWith('publish')
    }.forEach { task ->
        task.doFirst {
            if (!project.hasProperty("version") || project.findProperty("version") == "unspecified") {
                throw new GradleException("Unable to publish without version property")
            }
        }
    }
}
